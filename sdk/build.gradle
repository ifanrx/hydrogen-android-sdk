apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

def sdkVersionCode = 51
def sdkVersionName = "1.3.1"

android {
    compileSdkVersion 28

    // 需要 java 8 的支持
    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 28
        versionCode sdkVersionCode
        versionName sdkVersionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            consumerProguardFiles 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            consumerProguardFiles 'proguard-rules.pro'
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

}

dependencies {

    testImplementation 'junit:junit:4.12'

    androidTestImplementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test:rules:1.2.0'
    androidTestImplementation 'org.hamcrest:hamcrest-library:1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'io.reactivex.rxjava2:rxjava:2.2.12'
    androidTestImplementation 'io.reactivex.rxjava2:rxandroid:2.1.1'

    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation("com.squareup.okhttp3:okhttp:3.13.1")
    implementation("com.squareup.retrofit2:retrofit:2.5.0")
    api("com.squareup.retrofit2:converter-gson:2.5.0")

    api 'com.tencent.mm.opensdk:wechat-sdk-android-without-mta:5.3.1'
    api 'com.sina.weibo.sdk:core:4.3.7:openDefaultRelease@aar'
    api fileTree(dir: 'libs', include: ['*.aar', '*.jar'])
}

// 把 sdk 发布到 bintray：./gradlew :sdk:clean :sdk:bintrayUpload
String getPropOrEnv(String key) {
    String value = System.getProperty(key, null);
    if (value == null) {
        value = System.getenv(key);
    }
    return value;
}

apply plugin: 'com.novoda.bintray-release'
publish {
    bintrayUser = getPropOrEnv("BAAS_SDK_BINTRAY_USER")
    bintrayKey = getPropOrEnv("BAAS_SDK_BINTRAY_KEY")
    userOrg = ""
    repoName = "minapp-android-sdk"

    // 项目的坐标
    groupId = "com.minapp.android"
    artifactId = "sdk"
    publishVersion = sdkVersionName

    desc = "minapp-android-sdk"
    uploadName = "minapp-android-sdk"
    website = "https://github.com/ifanrx/hydrogen-android-sdk"
    dryRun = false
}
